external procedure FindNextIVVcOfficialSerialNr(var record IVVc);
external function Boolean HasModIN();
external function Boolean IsPostcodeFormatCorrect(string);
external procedure NextM4Number(string,var string);
external function Boolean GetCustAndBal(var record CUVc,var val,var val,Integer,Integer,Integer,Integer,Integer,Integer,var Boolean);
external function boolean ValidateIBAN(string);
external procedure SubPrePayments(record IVVc,var val,var val);
external procedure SubCashRows_IVVc(record IVVc,Boolean,var val,var val);
external function Integer GetVATLaw();
external function Boolean AnyHistoryAfterInvoice(record IVVc);
external function Boolean AcceptanceRulesExists(Integer,string);
external procedure IVVc_PasteLoyaltyCardNr(var record IVVc,record LocalMachineBlock);
external procedure WarnFutureDate(Boolean,Date);
external function Boolean GetPurchaseItem(string,string,var record PIVc);
external procedure IVSumup(var record IVVc,Boolean);
external function string 255 FindNextLegalSerNr(record IVVc,string);
external procedure IVOpenPrepExists(string);
external function val GetINCostPrice(string,string,Boolean);
external procedure PastePDInInv(var record IVVc,string,Boolean);
external function string 255 GetObjectFromRestIT(string);
external function string 255 CurDrawerCode(string);
external procedure GetSalesGroup(string,var string);
external procedure GetFullCurncyRate(var string,Date,var val,var val,var val,var val,var val);
external procedure GetCurUser(var record UserVc);
external function string 255 FindReservationCancellationTerms(string,Date,string);
external function LongInt DateDiff(Date,Date);
external updating function LongInt PUCheckIfSaveAllowed(var record PUVc,record PUVc,LongInt,LongInt);
external procedure PUCalcCostPrice(string,val,Integer,Integer,string,string,
                                   val,val,val,val,val,
                                   val,val,val,val,val,val,
                                   string,var val,val,var val,string,Integer);
external procedure PUCalcPerc(val,string,var val);
external procedure PUSumUp(var record PUVc);
external function val MulRateToBase1(var string,val,val,val,val,val,val,roundmode);
external function string 255 FindINObjects(string,string);
external procedure GetItemVATCode(string,Integer,var string,Boolean);
external function val DivRateToBase1(string,val,val,val,val,val,val,roundmode);
external function Boolean FindItemVAR(string,var string,var string,var string,var string);
external function Boolean ReadFirstItem(string,var record INVc,Boolean,Boolean);
external function Boolean CheckInternetEnabler();
external procedure StripSpace(var string,string);
external function Boolean IsDigit(string);
external procedure ReverseString(string,var string);
external function Boolean CurencyCodeIsISO(string);
external function Boolean CustomerCountryCodeIsISO(string);
external function Boolean InvoiceToEInvoiceQueueTest(Integer,string,string,var record EInvCUVc);
external function Boolean ExistStockTrans(string,Date,var Integer,var string,string,LongInt,record MainStockBlock);
remote function boolean ArgEInvoiceBeingSentRemote(longint,string,Boolean);

function string 10 GetIVCostAcc(record INVc INr,string vecode)//it is for automatic creation on Goods Receipt from Invoice, it must Credit Cost Account according to current settings
BEGIN
  string 10 CostAcc;
  record AccBlock ARAccRec;
  record CostAccBlock CostAccRec;
  record ITVc ITr;
  record VEVc VEr;
  integer vtype;

  BlockLoad(ARAccRec);
  BlockLoad(CostAccRec);
  VEr.Code = vecode;
  if ReadFirstMain(VEr,1,true) then begin
    if (VEr.ExportFlag==0) then begin
      vtype = 0;
    end;
    if (VEr.ExportFlag==1) or (VEr.ExportFlag==3) then begin
      vtype = 1;
    end;
    if (VEr.ExportFlag==2) or (VEr.ExportFlag==4) then begin
      vtype = 2;
    end;  
  end;
  if (nonblank(ARAccRec.StockCostAcc)) then begin
    if (vtype<=0) then begin
      CostAcc = ARAccRec.StockCostAcc;
    end;
  end;  
  if (nonblank(ARAccRec.EUStockCostAcc)) then begin
    if (vtype==1)  or (vtype==3) then begin
      CostAcc = ARAccRec.EUStockCostAcc;
    end;   
  end;  
  if (nonblank(ARAccRec.ExpStockCostAcc)) then begin
    if (vtype==2)  or (vtype==4) then begin
      CostAcc = ARAccRec.ExpStockCostAcc;
    end;    
  end;   
  
  if (nonblank(ARAccRec.StockCostAcc)) then begin
    CostAcc = ARAccRec.StockCostAcc;
  end;   
  ITr.Code = INr.Group;
  if (nonblank(ITr.Code)) then begin
    if (ReadFirstMain(ITr,1,true)) then begin
    end;
  end;  
  if (CostAccRec.ItemGroupAccounts!=0) then begin
    if (nonblank(ITr.CostAcc)) then begin
      if (vtype<=0) then begin
        CostAcc = ITr.CostAcc;
      end; 
    end;
    if (nonblank(ITr.EUCostAcc)) then begin
      if (vtype==1)  or (vtype==3) then begin
        CostAcc = ITr.EUCostAcc;
      end;
    end;
    if (nonblank(ITr.ExpCostAcc)) then begin
      if (vtype==2)  or (vtype==2) then begin
        CostAcc = ITr.ExpCostAcc;
      end;   
    end;  
  end else begin// Delivery works one way and Invoice another Many people is not happy except one man I guess
    if (nonblank(INr.CostAcc)) then begin
      if (vtype<=0) then begin
        CostAcc = INr.CostAcc;
      end; 
    end;
    if (nonblank(INr.EUCostAcc)) then begin
      if (vtype==1)  or (vtype==3) then begin
        CostAcc = INr.EUCostAcc;
      end;
    end;
    if (nonblank(INr.ExpCostAcc)) then begin
      if (vtype==2)  or (vtype==2) then begin
        CostAcc = INr.ExpCostAcc;
      end;   
    end;  
  end;
  GetIVCostAcc = CostAcc;
  RETURN;
END;

global
function Boolean CheckAddr3CorrectforEInvoicing(string addr3)
begin
  Boolean res,addedf;
  string 255 tstr,c;
  Integer i;
  string 255 postcode,city;
  
  res = true;
  StripSpace(tstr,addr3);
  if (len(tstr)==len(addr3)) then begin
    res = false;
    goto LCheckAddr3CorrectforEInvoicing;
  end;  

  for (i=0;i<len(tstr);i=i+1) begin
    addedf = false;
    c = Mid(tstr,i,1);
    if (IsDigit(c)) then begin
      postcode = postcode & c;      
      addedf = true;
    end;
    if (c=="-") then begin
      postcode = postcode & c;      
      addedf = true;
    end;
    if (addedf==false) then begin
      i = len(tstr);
    end;
  end;  
  city = Right(addr3,len(addr3)-len(postcode));
  if (blank(postcode)) then begin
    res = false;
    goto LCheckAddr3CorrectforEInvoicing;
  end;  
  if (len(postcode)>9) then begin
    res = false;
    goto LCheckAddr3CorrectforEInvoicing;
  end;  
  for (i=0;i<len(postcode);i=i+1) begin
    c = Mid(postcode,i,1);
    if (IsDigit(c)==false) then begin
      res = false;
      goto LCheckAddr3CorrectforEInvoicing;
    end;
  end;
LCheckAddr3CorrectforEInvoicing:;  
  CheckAddr3CorrectforEInvoicing = res;
  return;
end;

global
function Boolean CheckCYBank1CorrectForEInvoicing(string bank1)
begin
  Boolean res,addedf;
  string 255 bankname,bankacc,c,tstr;
  Integer i;
  
  res = true;
  if (ValidateIBAN(bank1)) then begin
    goto LCheckCYBank1CorrectForEInvoicing;
  end;
  ReverseString(bank1,tstr);
  for (i=0;i<len(tstr);i=i+1) begin
    addedf = false;
    c = Mid(tstr,i,1);
    if (IsDigit(c)) then begin
      bankacc = bankacc & c;      
      addedf = true;
    end;
    if (c=="-") then begin
      bankacc = bankacc & c;      
      addedf = true;
    end;
    if (addedf==false) then begin
      i = len(tstr);
    end;
  end;  
  ReverseString(bankacc,bankacc);  
  bankname = Left(bank1,len(bank1)-len(bankacc));
  if (blank(bankname)) then begin
    res = false;
    goto LCheckCYBank1CorrectForEInvoicing;
  end;
  if (len(bank1)<=10) then begin
    res = false;
    goto LCheckCYBank1CorrectForEInvoicing;
  end;
  for (i=0;i<6;i=i+1) begin
    c = Mid(bankacc,i,1);
    if (IsDigit(c)==false) then begin
      res = false;
      goto LCheckCYBank1CorrectForEInvoicing;
    end;
  end;
  if (Mid(bankacc,6,1)!="-") then begin
    res = false;
    goto LCheckCYBank1CorrectForEInvoicing;
  end;
  for (i=7;i<len(bankacc);i=i+1) begin
    c = Mid(bankacc,i,1);
    if (IsDigit(c)==false) then begin
      res = false;
      goto LCheckCYBank1CorrectForEInvoicing;
    end;
  end;
LCheckCYBank1CorrectForEInvoicing:;  
  CheckCYBank1CorrectForEInvoicing = res;
  return;
end;

function Boolean ValidEInvoiceData_Sweden(record IVVc IVr,record CYBlock CYb,record CUVc CUr,record EInvCUVc EInvCUr,record BaseCurBlock BCb,var Integer errcode,var string gotofield)
begin
  Boolean res;
  Boolean testf;
  record EInvoiceBlock EIb;
  
  res = true;
  
  if (EInvCUr.RcvInvoicePref==kEInvoiceRcvPreferenceElectronic and EInvCUr.AttachPDFtoElectronicInvoice) then begin
    if (blank(CUr.eMail) and blank (EInvCUr.eMail)) then begin
      errcode = 20475;
      gotofield = "CustCode";
      res = false;
    end;
    goto LValidEInvoiceData_Sweden;
  end;
  
  if (CustomerCountryCodeIsISO(IVr.CustCode)==false) then begin
    errcode = 20590;
    gotofield = "CustCode";
    res = false;
    goto LValidEInvoiceData_Sweden;
  end;
  if (blank(IVr.CurncyCode)) then begin
    errcode = 1058;
    gotofield = "CurncyCode";
    res = false;
    goto LValidEInvoiceData_Sweden;
  end;
  if (CurencyCodeIsISO(IVr.CurncyCode)==false) then begin
    errcode = 20591;
    gotofield = "CurncyCode";
    res = false;
    goto LValidEInvoiceData_Sweden;
  end;
  if (BCb.BaseCur1!="SEK" and BCb.BaseCur1!="EUR") then begin
    errcode = 20670;
    gotofield = "SerNr";
    res = false;
    goto LValidEInvoiceData_Sweden;
  end;
  if (ReadFirstMain(EInvCUr,1,true)) then begin 
    if (EInvCUr.RcvInvoicePref!=kEInvoiceRcvPreferencePaper) then begin
      testf = true;
    end;
  end else begin
    BlockLoad(EIb);
    if (EIb.RcvInvoicePref!=kEInvoiceRcvPreferencePaper) then begin 
      testf = true;
    end;
  end;
  if (testf) then begin
    if (blank(IVr.VATNr) and blank(CUr.RegNr1) and blank(CUr.RegNr2)) then begin
      errcode = 20592;
      gotofield = "VATNr";
      res = false;
      goto LValidEInvoiceData_Sweden;
    end;
  end;
  if (blank(CYb.VATNr)) and (blank(CYb.OrgNr)) and (blank(CYb.OrgNr2)) then begin
    errcode = 20593;
    gotofield = "VATNr";
    res = false;
    goto LValidEInvoiceData_Sweden;
  end;
  if (CUr.FTax!=0 and (blank(CUr.RegNr1) and blank(CUr.RegNr2))) then begin
    errcode = 20671;
    gotofield = "VATNr"; //Customer Code field???
    res = false;
    goto LValidEInvoiceData_Sweden;
  end;
LValidEInvoiceData_Sweden:;  
  ValidEInvoiceData_Sweden = res;
  return;
end;

function Boolean ValidEInvoiceData_Finland(record IVVc IVr,record CYBlock CYb,record CUVc CUr,record EInvCUVc EInvCUr,record BackupBlock Bkpb,
                                           record BaseCurBlock BCb,
                                           var Integer errcode,var string gotofield)
begin
  Boolean res;
  Integer RcvInvoicePref;
  record EInvoiceBlock EIb;
  
  res = true;
  if (nonblank(EInvCUr.CustCode)) then begin
    RcvInvoicePref = EInvCUr.RcvInvoicePref;
  end;
  if (RcvInvoicePref==0) then begin
    BlockLoad(EIb);
    RcvInvoicePref = EIb.RcvInvoicePref;
  end;
  if (len(IVr.ClientContact)>35) then begin
    errcode = 20793;
    gotofield = "ClientContact";
    res = false;
    goto LValidEInvoiceData_Finland;
  end;
  if (len(IVr.CustOrdNr)>35) then begin
    errcode = 20793;
    gotofield = "CustOrdNr";
    res = false;
    goto LValidEInvoiceData_Finland;
  end;
  if (CheckCYBank1CorrectForEInvoicing(CYb.Bank1)==false) then begin
    errcode = 20584;
    gotofield = "VATNr";
    res = false;
    goto LValidEInvoiceData_Finland;
  end;
  if (RcvInvoicePref!=kEInvoiceRcvPreferencePaper) then begin
    if (blank(EInvCUr.EInvoiceAccount)) then begin
      errcode = 20587;
      gotofield = "VATNr";
      res = false;
      goto LValidEInvoiceData_Finland;
    end;
  end else begin
    if (blank(EInvCUr.EInvoiceAccount)) and (blank(CUr.ANACode)) and (CUr.CustType==0) then begin
      errcode = 20587;
      gotofield = "VATNr";
      res = false;
      goto LValidEInvoiceData_Finland;
    end;    
  end;
  if (blank(CYb.VATNr)) then begin
    errcode = 20586;
    gotofield = "VATNr";
    res = false;
    goto LValidEInvoiceData_Finland;
  end;
  if (blank(CYb.OrgNr)) then begin
    errcode = 20586;
    gotofield = "VATNr";
    res = false;
    goto LValidEInvoiceData_Finland;
  end;
  if (RcvInvoicePref!=2) then begin
    if (blank(CYb.ANACode)) then begin
      errcode = 20585;
      gotofield = "VATNr";
      res = false;
     goto LValidEInvoiceData_Finland;
    end;
  end;
  if (blank(CYb.EInvoiceAccount)) then begin
    errcode = 20583;
    gotofield = "VATNr";
    res = false;
    goto LValidEInvoiceData_Finland;
  end;
  if (blank(CYb.Bank1)) then begin
    errcode = 20598;
    gotofield = "VATNr";
    res = false;
    goto LValidEInvoiceData_Finland;
  end;
//  if (blank(Bkpb.HTSConnectionTime)) then begin
//    errcode = 20599;
//    gotofield = "SerNr";
//    res = false;
//    goto LValidEInvoiceData_Finland;
//  end;
//  if (Bkpb.HTSBackgroundTimer<=0) then begin
//    errcode = 20599;
//    gotofield = "SerNr";
//    res = false;
//    goto LValidEInvoiceData_Finland;
//  end;
  if (CheckAddr3CorrectforEInvoicing(IVr.Addr3)==false) then begin
    errcode = 20582;
    gotofield = "Addr3";
    res = false;
    goto LValidEInvoiceData_Finland;
  end;
  if (blank(IVr.CurncyCode)) then begin
    errcode = 1582;
    gotofield = "CurncyCode";
    res = false;
    goto LValidEInvoiceData_Finland;
  end;
/*  
  if (IVr.CurncyCode!=BCb.BaseCur1) then begin
    errcode = 1582;
    gotofield = "CurncyCode";
    res = false;
    goto LValidEInvoiceData_Finland;
  end;
*/  
  if (CurencyCodeIsISO(IVr.CurncyCode)==false) then begin
    errcode = 20591;
    gotofield = "CurncyCode";
    res = false;
    goto LValidEInvoiceData_Finland;
  end;
  if (CheckInternetEnabler==false) then begin
    errcode = 20242;
    gotofield = "SerNr";
    res = false;
    goto LValidEInvoiceData_Finland;
  end;
  if (RcvInvoicePref!=kEInvoiceRcvPreferencePaper) or (CUr.CustType==0) then begin
    if (blank(IVr.VATNr)) then begin
      errcode = 20592;
      gotofield = "VATNr";
      res = false;
      goto LValidEInvoiceData_Finland;
    end;
    if (blank(CUr.ANACode)) then begin
      errcode = 20588;
      gotofield = "VATNr";
      res = false;
      goto LValidEInvoiceData_Finland;
    end;
  end;
  if (blank(IVr.Addr2)) or (blank(IVr.Addr3)) then begin
    errcode = 1058;
    gotofield = "Addr1";
    res = false;
    goto LValidEInvoiceData_Finland;
  end;
  if (nonblank(IVr.LangCode)) then begin
  if (IVr.LangCode!="EN") and (IVr.LangCode!="FI") and (IVr.LangCode!="SE") then begin
    errcode = 2246;
    gotofield = "LangCode";
    res = false;
    goto LValidEInvoiceData_Finland;
  end;
  end;
LValidEInvoiceData_Finland:;  
  ValidEInvoiceData_Finland = res;
  return;
end;

function Boolean ValidEInvoiceData_Norway(record IVVc IVr,record CYBlock CYb,record CUVc CUr,record EInvCUVc EInvCUr,record BaseCurBlock BCb,var Integer errcode,var string gotofield)
begin
  Boolean res;
  
  res = true;
  if (CustomerCountryCodeIsISO(IVr.CustCode)==false) then begin
    errcode = 20590;
    gotofield = "CustCode";
    res = false;
    goto LValidEInvoiceData_Norway;
  end;
  if (blank(IVr.CurncyCode)) then begin
    errcode = 1058;
    gotofield = "CurncyCode";
    res = false;
    goto LValidEInvoiceData_Norway;
  end;
  if (CurencyCodeIsISO(IVr.CurncyCode)==false) then begin
    errcode = 20591;
    gotofield = "CurncyCode";
    res = false;
    goto LValidEInvoiceData_Norway;
  end;
  if (blank(IVr.VATNr)) and (blank(CUr.RegNr1)) and (blank(CUr.RegNr2)) then begin
    errcode = 20592;
    gotofield = "VATNr";
    res = false;
    goto LValidEInvoiceData_Norway;
  end;
  if (blank(CYb.VATNr)) and (blank(CYb.OrgNr)) and (blank(CYb.OrgNr2)) then begin
    errcode = 20593;
    gotofield = "VATNr";
    res = false;
    goto LValidEInvoiceData_Norway;
  end;
LValidEInvoiceData_Norway:;  
  ValidEInvoiceData_Norway = res;
  return;
end;

global
function Boolean ValidEInvoiceData(record IVVc IVr,record CUVc CUr,var Integer errcode,var string gotofield)
begin
  Boolean res;
  record InternetEnablerBlock IEb;
  record CYBlock CYb;
  record BaseCurBlock BCb;
  record BackupBlock Bkpb;
  record EInvCUVc EInvCUr;  
  
  res = true;
  if (IVr.NoEInvoice==0) then begin
  if (InvoiceToEInvoiceQueueTest(0,CUr.Code,IVr.CurncyCode,EInvCUr)) then begin
    BlockLoad(IEb);
    BlockLoad(CYb);
    BlockLoad(BCb);
    BlockLoad(Bkpb);
    switch (IEb.RegInCountry) begin
      case 2:
        goto LLValidEInvoiceData01;
      case 3:
        res = ValidEInvoiceData_Sweden(IVr,CYb,CUr,EInvCUr,BCb,errcode,gotofield);
      case 4:
        res = ValidEInvoiceData_Finland(IVr,CYb,CUr,EInvCUr,Bkpb,BCb,errcode,gotofield);
      case 26:
      LLValidEInvoiceData01:;
        res = ValidEInvoiceData_Norway(IVr,CYb,CUr,EInvCUr,BCb,errcode,gotofield);
      case 99://HansaMail
        switch (CurrentCompany) begin
          case 24:
            res = ValidEInvoiceData_Sweden(IVr,CYb,CUr,EInvCUr,BCb,errcode,gotofield);
          case 39:
            res = ValidEInvoiceData_Sweden(IVr,CYb,CUr,EInvCUr,BCb,errcode,gotofield);
          case 40:
            res = ValidEInvoiceData_Finland(IVr,CYb,CUr,EInvCUr,Bkpb,BCb,errcode,gotofield);
          case 82:
            res = ValidEInvoiceData_Sweden(IVr,CYb,CUr,EInvCUr,BCb,errcode,gotofield);
        end;
    end;
  end;  
  end;
LValidEInvoiceData:;  
  ValidEInvoiceData = res;
  return;
end;

function Boolean ValidInvoiceDataForVATLaw_Portuguese(record IVVc IVr,record CUVc CUr,var Integer errcode,var string gotofield)
begin
  Boolean res;
  val rval,rvalb1;
  Boolean testf;
  record CountryVc Countryr;

  res = true;
  if (blank(IVr.VATNr)) then begin
    errcode = 20275;
    gotofield = "VATNr";
    res = false;
    goto LValidInvoiceDataForVATLaw_Portuguese;
  end;
//  if (IVr.InvType!=kInvoiceTypeCash) and (IVr.InvType!=kInvoiceTypeCredit) and (IVr.InvType!=kInvoiceTypeCreditSpecialSales) then begin
/*
do not put it back please
  if (IVr.InvType==kInvoiceTypeCash) then begin
//what is this code for ? 
    rval = IVr.Sum4;
    rvalb1 = IVr.BaseSum4;
    SubCashRows_IVVc(IVr,true,rval,rvalb1);
    SubPrePayments(IVr,rval,rvalb1);
    if (rval!=0) then begin
      errcode = 1958;
      gotofield = "PayDeal";
      res = false;
      goto LValidInvoiceDataForVATLaw_Portuguese;
    end;
  end;
*/
  testf = true;
  if (IVr.VATNr=="999999990") then begin testf = false; end;
  if (CUr.CustType==1) then begin
    if (IVr.Sum1<=1000) then begin
      if (blank(IVr.Addr1) and blank(IVr.Addr2) and blank(IVr.Addr3) and blank(IVr.InvAddr3) and blank(IVr.InvAddr4)) then begin
        testf = false;
      end;
    end;
  end else begin
    if (IVr.InvType==kInvoiceTypeCash or IVr.InvType==kInvoiceTypeCashInvoiceReceiptPRT) then begin testf = false; end;
  end;
  if (testf) then begin
    testf = false;
    if (blank(IVr.Addr1) and blank(IVr.Addr2)) then begin
      gotofield = "Addr1";
      testf = true;
    end;
    if (blank(IVr.Addr3)) then begin
      gotofield = "Addr3";
      testf = true;
    end;
    if (blank(IVr.InvAddr3)) then begin
      gotofield = "InvAddr3";
      testf = true;
    end;
    if (blank(IVr.InvAddr4)) then begin
      gotofield = "InvAddr4";
      testf = true;
    end;
    if (testf) then begin
      errcode = 20276;
      res = false;
      goto LValidInvoiceDataForVATLaw_Portuguese;
    end;
  end;
  if (nonblank(IVr.InvAddr4)) then begin
    Countryr.Comment = IVr.InvAddr4;
    if (ReadFirstKey("Comment",Countryr,1,true)==false) then begin
      errcode = 20277;
      res = false;
      gotofield = "InvAddr4";
      goto LValidInvoiceDataForVATLaw_Portuguese;
    end;
  end;
  if (blank(IVr.InvAddr4)) or (IVr.InvAddr4=="Portugal") then begin
    if (nonblank(IVr.Addr3)) then begin
      if (IsPostcodeFormatCorrect(IVr.Addr3)==false) then begin
        errcode = 24620;
        res = false;
        gotofield = "Addr3";
        goto LValidInvoiceDataForVATLaw_Portuguese;
      end;
    end;
  end;

  if (IVr.VATNr!="999999990") then begin
    if (nonblank(IVr.ShipAddr1) or nonblank(IVr.ShipAddr2) or nonblank(IVr.DelAddr3) or nonblank(IVr.DelAddr4)) then begin
      testf = false;
      if (blank(IVr.ShipAddr1) and blank(IVr.ShipAddr2)) then begin
        gotofield = "ShipAddr1";
        testf = true;
      end;
      if (blank(IVr.ShipAddr3)) then begin
        gotofield = "ShipAddr3";
        testf = true;
      end;
      if (blank(IVr.DelAddr3)) then begin
        gotofield = "DelAddr3";
        testf = true;
      end;
      if (blank(IVr.DelAddr4)) then begin
        gotofield = "DelAddr4";
        testf = true;
      end;
      if (testf) then begin
        errcode = 20276;
        res = false;
        goto LValidInvoiceDataForVATLaw_Portuguese;
      end;
    end;
  end;

  if (nonblank(IVr.DelAddr4)) then begin
    Countryr.Comment = IVr.DelAddr4;
    if (ReadFirstKey("Comment",Countryr,1,true)==false) then begin
      errcode = 20277;
      res = false;
      gotofield = "DelAddr4";
      goto LValidInvoiceDataForVATLaw_Portuguese;
    end;
  end;
  if (blank(IVr.DelAddr4)) or (IVr.DelAddr4=="Portugal") then begin
    if (nonblank(IVr.ShipAddr3)) then begin
      if (IsPostcodeFormatCorrect(IVr.ShipAddr3)==false) then begin
        errcode = 24620;
        res = false;
        gotofield = "ShipAddr3";
        goto LValidInvoiceDataForVATLaw_Portuguese;
      end;
    end;
  end;

LValidInvoiceDataForVATLaw_Portuguese:;
  ValidInvoiceDataForVATLaw_Portuguese = res;
  return;
end;

global
function Boolean ValidInvoiceDataForVATLaw(record IVVc IVr,record CUVc CUr,var Integer errcode,var string gotofield)
begin
  Boolean res;
  
  res = true;
  switch (GetVATLaw) begin
    case vatPortuguese:
      res = ValidInvoiceDataForVATLaw_Portuguese(IVr,CUr,errcode,gotofield);
  end;
LValidInvoiceDataForVATLaw:;  
  ValidInvoiceDataForVATLaw = res;
  return;
end;

procedure DeductIVPrePay(record IVVc IVp,var val tp)
BEGIN
  Integer rwcnt,i;
  row IVVc IVrw;
  
  rwcnt = MatRowCnt(IVp);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(IVp,i,IVrw);
    if (IVrw.stp==6) then begin
      tp = tp - IVrw.Sum;
    end;
  end;
  RETURN;
END;

function Boolean IVVcRecordInvalidateTest_Check(record IVVc IVr,var Integer errcode,LongInt check)
begin
  Boolean res;
  row IVVc IVrw;
  Integer i,rwcnt,primary;
  record CostAccBlock CAb;
  record MainStockBlock MSb;
  string 255 errstr;
  record INVc INr;
  Boolean testihf;
  
  res = true;
  BlockLoad(CAb);
  if (CAb.RetFIFOQue!=0) then begin
    res = false;
    errcode = 20558;
    goto LIVVcRecordInvalidateTest_Check;
  end;
  if (IVr.InvType==kInvoiceTypeCredit or IVr.InvType==kInvoiceTypeCreditSpecialSales) then begin//could be better
    res = false;
    errcode = 20569;
    goto LIVVcRecordInvalidateTest_Check;
  end;
  
  rwcnt = MatRowCnt(IVr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(IVr,i,IVrw);
    if (IVrw.stp==kInvoiceRowTypeNormal) or (IVrw.stp==kInvoiceRowTypeCorrection) or (IVrw.stp==kInvoiceRowTypeStructuredItemComponent) then begin
      if (nonblank(IVrw.ArtCode)) then begin
        if (ReadFirstItem(IVrw.ArtCode,INr,false,false)) then begin
          if (INr.ItemType==kItemTypeStocked) or (INr.ItemType==kItemTypeStructured) then begin
            testihf = true;
            i = rwcnt;
          end;
        end;
/*
        primary = ArtCodePrimaryCostModel(IVrw.ArtCode);
        switch (primary) begin
          case 0: ;
          case 9: ;
          otherwise
            res = 0;
            goto LIVVcRecordInvalidateTest_Check;
        end;
*/
      end;
    end;
  end;
  if (testihf) then begin
    BlockLoad(MSb);
    if (MSb.Chronology!=0) then begin
      if (ExistStockTrans(IVr.Location,CurrentDate,errcode,errstr,"IVVc",IVr.SerNr,MSb)) then begin
        errcode = 21300;
        res = false;
        goto LIVVcRecordInvalidateTest_Check;
      end;
    end;
    if (AnyHistoryAfterInvoice(IVr)) then begin
      errcode = 21301;
      res = false;
      goto LIVVcRecordInvalidateTest_Check;
    end;
  end;
LIVVcRecordInvalidateTest_Check:;  
  IVVcRecordInvalidateTest_Check = res;
  return;
end;

function Boolean UserCanInvalidateIV()
begin
  Boolean res,HasInvInvalid,HasIVInvalid;
  
  res = true;
  
  HasInvInvalid = UserCanAction("InvInvalid",true)==UserCanAction("InvInvalid",false);
  HasIVInvalid = UserCanAction("IVInvalid",true)==UserCanAction("IVInvalid",false);
  
  if (HasInvInvalid) then begin
    res = UserCanAction("InvInvalid",true);
  end;
  
  if (HasIVInvalid) then begin
    res = UserCanAction("IVInvalid",true);
  end;
  
  UserCanInvalidateIV = res;
end;

global
function LongInt IVVcRecordInvalidateTest(var record IVVc IVr,record IVVc IV2r,LongInt long3,LongInt long4)
BEGIN
  LongInt res;
  record ARVc ARr;
  record DBLockBlock DBLb;
  val t;
  Integer errcode;
  
  res = 1;
  if (UserCanInvalidateIV==false) then begin
    res = 0;
    MessageBox(34500,"");
    goto LIVVcRecordInvalidateTest;
  end;
  if (IVr.Invalid!=0) then begin
    res = 0;
    goto LIVVcRecordInvalidateTest;
  end;
  if (IVr.OKFlag==0) then begin
    res = 0;
    MessageBox(34500,"");
    goto LIVVcRecordInvalidateTest;
  end;
  if (nonblank(IVr.CAE)) then begin
    res = 0;
    MessageBox(2911,"");
    goto LIVVcRecordInvalidateTest;
  end;
  if (HasLocalization("ARG")) then begin
    if (ArgEInvoiceBeingSentRemote(IVr.SerNr,IVr.OfficialSerNr,false)) then begin
      res = 0;
      MessageBox(2914,"");
      goto LIVVcRecordInvalidateTest;
    end;
  end;
  if (IVr.UpdStockFlag!=0) then begin
    if (IVVcRecordInvalidateTest_Check(IVr,errcode,long4)==false) then begin
      if (errcode>0) then begin
        MessageBox(errcode,"");
      end else begin
        MessageBox(34500,"");
      end;
      res = 0;
      goto LIVVcRecordInvalidateTest;
    end;
  end;
  BlockLoad(DBLb);
  if (IVr.OKFlag!=0) then begin
    if (IVr.TransDate<=DBLb.SLLock) then begin
      if (long3>0) then begin
        MessageBox(1560,"");
      end else begin
        MessageBox(34500,"");
      end;
      res = 0;
      goto LIVVcRecordInvalidateTest;
    end;
    if (IVr.TransDate<=DBLb.SLWarn) then begin
      if (long3>0) then begin
        MessageBox(1560,"");
      end;
    end;
  end;
  switch (IVr.InvType) begin
    case kInvoiceTypeNormalSpecialSales: goto LkInvoiceTypeNormal;
    case kInvoiceTypeNormal:
      LkInvoiceTypeNormal:;
      ARr.InvoiceNr = IVr.SerNr;
      t = IVr.Sum4;
      DeductIVPrePay(IVr,t);
      if (ReadFirstMain(ARr,1,true)) then begin
        if (ARr.RVal==t) then begin    
          IVr.Invalid = 1;
        end;  
      end else begin
        if (t==0) then begin    
          IVr.Invalid = 1;
        end else begin
          IVr.Invalid = 0;
          res = 0;
          MessageBox(34500,"");
        end;  
      end;
    case kInvoiceTypeCashInvoiceReceiptPRT:
      IVr.Invalid = 1;
    case kInvoiceTypeCash:
      IVr.Invalid = 1;
    case kInvoiceTypeCreditSpecialSales: goto LkInvoiceTypeCredit;
    case kInvoiceTypeCredit:
      LkInvoiceTypeCredit:;
      if (IVr.UpdStockFlag==0) then begin
        IVr.Invalid = 1;
      end;
    case kInvoiceTypeInterest:
      ARr.InvoiceNr = IVr.SerNr;
      t = IVr.Sum4;
      if (ReadFirstMain(ARr,1,true)) then begin
        if (ARr.RVal==t) then begin    
          IVr.Invalid = 1;
        end;  
      end else begin
        if (t==0) then begin    
          IVr.Invalid = 1;
        end else begin
          IVr.Invalid = 0;
          res = 0;
          MessageBox(34500,"");
        end;  
      end;
    case kInvoiceTypeDownpayment:
      ARr.InvoiceNr = IVr.SerNr;
      t = IVr.Sum4;
      DeductIVPrePay(IVr,t);
      if (ReadFirstMain(ARr,1,true)) then begin
        if (ARr.RVal==t) then begin    
          IVr.Invalid = 1;
        end;  
      end else begin
        if (t==0) then begin    
          IVr.Invalid = 1;
        end else begin
          IVr.Invalid = 0;
          res = 0;
          MessageBox(34500,"");
        end;  
      end;
  end;
LIVVcRecordInvalidateTest:;
  if (res!=0 and IVr.Invalid==0) then begin
    res = 0;
    MessageBox(34500,"");
  end;
  if (res==1) then begin
    IVr.Status = kRecordStatusNone;
  end;
  IVVcRecordInvalidateTest = res;  
  RETURN;
END;

function Boolean PasteArtCodeToPU(var record PUVc PUr,Integer rownr,row IVVc IVrw)
BEGIN
  Boolean res;
  record CostAccBlock CostAccRec;
  record PIVc PIr;
  record INVc INr;
  row PUVc PUrw;
  Boolean chsum,chrsum;
  string 60 sz,msk,mskrep,pcstr,tstr,location;
  val t,p,s,t2;
  record SysFormatBlock SysFormatRec;
  string 200 varsubset;
  
  PUrw.ArtCode = IVrw.ArtCode;
  PUrw.Quant = IVrw.Quant;
  if (ReadFirstItem(PUrw.ArtCode,INr,true,true)) then begin
    BlockLoad(SysFormatRec);
    if (FindItemVAR(INr.Code,sz,msk,mskrep,varsubset)) then begin INr.Code = PUrw.ArtCode; end;
    BlockLoad(CostAccRec);    
    pcstr = "";

    PUrw.SerialNr = IVrw.SerialNr;
    PUrw.Location = IVrw.Location;
    PUrw.Spec = IVrw.Spec;
    PUrw.UPrice = IVrw.FIFO;
    p = IVrw.FIFO;
    if (INr.PriceFactor!=0) then begin
      p = p/INr.PriceFactor;
    end;
    p = DivRateToBase1(PUr.CurncyCode,p,PUr.FrRate,PUr.ToRateB1,PUr.ToRateB2,PUr.BaseRate1,PUr.BaseRate2,DefaultCurRoundOff);

    GetItemVATCode(PUrw.ArtCode,PUr.ExportFlag,tstr,false);
    PUrw.VATCode = tstr;
    PUrw.Coefficient = INr.UnitCoefficient;
    PUrw.ArtCode = INr.Code;
    PUrw.Spec = INr.Name;
    PUrw.Objects = FindINObjects(INr.Objects,INr.Group);
    PUrw.UPrice = p;
    PUrw.UnitXval = INr.Width;
    PUrw.UnitYval = INr.Height;
    PUrw.UnitZval = INr.Depth;
    PUrw.VATCode = INr.VATCode;
    t2 = MulRateToBase1(PUr.CurncyCode,p,PUr.FrRate,PUr.ToRateB1,PUr.ToRateB2,PUr.BaseRate1,PUr.BaseRate2,DefaultCurRoundOff);
    PUCalcPerc(t2,PUrw.CustomsCost,t2);
    pcstr = ValToString(t2,M45Val,SysFormatRec.thousSep,SysFormatRec.decimalPt,0);
    PUrw.CustomsCost = pcstr;
    PUrw.BasePrice = INr.UPrice1;
    tstr = "0";
    PUrw.Extra = tstr;
    location = PUrw.Location;
    if (blank(location)) then begin
      location = PUr.Location;
    end;
    GetPurchaseItem(PUrw.ArtCode,location,PIr);
    if (PIr.PIFactor!=0) then begin
      PUrw.UPrice = PUrw.UPrice/PIr.PIFactor;
      PUrw.BasePrice = PUrw.BasePrice/PIr.PIFactor;
      PUrw.PIFactor = PIr.PIFactor;
    end;
    PUrw.CredAcc = GetIVCostAcc(INr,PUr.VECode);
    MatRowPut(PUr,rownr,PUrw);
    chrsum = true;
    res = true;
  end;
  if (chrsum) then begin
    PUCalcCostPrice(PUrw.ArtCode,PUrw.UPrice,PUr.InclVAT,PUr.NoTAXonVAT,PUrw.Extra,PUr.CurncyCode,
                    PUr.FrRate,PUr.ToRateB1,PUr.ToRateB2,PUr.BaseRate1,PUr.BaseRate2,
                    PUrw.ShipCost,PUrw.RowCost1,PUrw.RowCost2,PUrw.RowCost3,PUrw.RowCost4,PUrw.RowCost5,
                    PUrw.CustomsCost,p,PUrw.Quant,s,PUrw.VATCode,PUr.ExportFlag);
    PUrw.CostPrice = p;
    PUrw.Sum = s;
    chsum = true;
  end;
  MatRowPut(PUr,rownr,PUrw);    
  if (chsum) then begin
    PUSumUp(PUr);
  end;
  PasteArtCodeToPU = res;
  RETURN;
END;

function Boolean OrgIVFromOrder(LongInt credinv)
BEGIN
  Boolean res;
  record IVVc IVr;
  
  IVr.SerNr = credinv;
  if (ReadFirstMain(IVr,1,true)) then begin
    if (IVr.OrderNr!=-1) then begin
      res = true;
    end;
  end;
  OrgIVFromOrder = res;
  RETURN;
END;

global
updating function LongInt PUFromCreditNote(var record IVVc IVr,Boolean savef)
BEGIN
  LongInt res;
  record PUVc PU2r;
  record PUVc PUr;
  row PUVc PUrw;
  row IVVc IVrw;
  Integer i,rwcnt; 
  Integer purwcnt; 
  record INVc INr;
  string 255 warning,inwarn;
  record ModuleBlock ModuleRec;
  
  res = 0;
  BlockLoad(ModuleRec);
  if (ModuleRec.PUFromCredUpdStock==0) then begin goto LPUFromCreditNote; end; 
  if (OrgIVFromOrder(IVr.CredInv)) then begin goto LPUFromCreditNote; end; 
  if (true) then begin
    IVr.UpdStockFlag = 0;
    RecordNew(PUr);
    PUr.Location = IVr.Location;
    PUr.CurncyCode = IVr.CurncyCode;
    PUr.FrRate = IVr.FrRate;
    PUr.ToRateB1 = IVr.ToRateB1;
    PUr.ToRateB2 = IVr.ToRateB2;
    PUr.BaseRate1 = IVr.BaseRate1;
    PUr.BaseRate2 = IVr.BaseRate2;
    PUr.Objects = IVr.Objects;
    rwcnt = MatRowCnt(IVr);
    for (i=0;i<rwcnt;I=I+1) begin
      MatRowGet(IVr,i,IVrw);
      if (IVrw.FIFO!=0) and (IVrw.Quant!=0) and ((IVrw.stp==kInvoiceRowTypeNormal) or (IVrw.stp==kInvoiceRowTypeStructuredItemComponent)) then begin
        if (ReadFirstItem(IVrw.ArtCode,INr,true,true)) then begin
          if (INr.ItemType==1) then begin
            ClearRow(PUr,PUrw,1);
            if (PasteArtCodeToPU(PUr,purwcnt,IVrw)) then begin
              purwcnt = purwcnt + 1;
            end;
          end;
        end;
      end;
    end;
    if (MatRowCnt(PUr)==0) then begin goto LPUFromCreditNote; end;
    if (PUr.SerNr==-1) then begin
      PUr.SerNr = NextSerNr("PUVc",PUr.TransDate,-1,false,"");
    end;
    if (PUr.SerNr!=-1) then begin
      PUr.OKFlag = 1;
      if (savef) then begin
        if (RecordStore(PUr,false)) then begin end;
      end else begin
        res = PUCheckIfSaveAllowed(PUr,PU2r,1,0);
      end;
    end;
  end;
LPUFromCreditNote:;
  PUFromCreditNote = res;
  RETURN;
END;

global
function Integer TestCreditNoteTerms(record IVVc CreditIVp,var string errstr)
begin
  Integer res;
  record IVVc orgIVr;
  record IVVc IVr;
  Boolean found;
  record CNDVc CNDr;
  row CNDVc CNDrw;
  record CUVc CUr;
  Integer i,rwcnt;
  string 255 cnterm;
  val cntermprc,totcredit,totcreditallowed;
  record JobVc Jobr;
  
  errstr = "";
  res = 0;
  if (CreditIVp.JobNr>0) then begin
    Jobr.SerNr = CreditIVp.JobNr;
    if (ReadFirstMain(Jobr,1,true)) then begin
      cnterm = FindReservationCancellationTerms(Jobr.CUCode,Jobr.TransDate,Jobr.ResStatus);
      if (blank(cnterm)) then begin
        goto LTestCreditNoteTerms;
      end;
      CNDr.Code = cnterm;
      if (ReadFirstMain(CNDr,1,true)) then begin
        rwcnt = MatRowCnt(CNDr);
        for (i=0;i<rwcnt;i=i+1) begin
          MatRowGet(CNDr,i,CNDrw);
          if (DateDiff(Jobr.TransDate,CurrentDate)<=CNDrw.Days) then begin
            cntermprc = CNDrw.Percentage;
            i = rwcnt;
          end;
        end;
        if (cntermprc!=0) then begin  
          totcredit = CreditIVp.Sum4;
          orgIVr.SerNr = CreditIVp.CredInv;
          ReadFirstMain(orgIVr,1,true);
          found = true;
          IVr.CredInv = CreditIVp.CredInv;
          while (LoopKey("CredInv",IVr,1,found)) begin
            if (IVr.CredInv!=CreditIVp.CredInv) then begin found = false; end;
            if (found) then begin
              if (IVr.SerNr!=CreditIVp.SerNr) then begin
                if (IVr.InvType==kInvoiceTypeCredit or IVr.InvType==kInvoiceTypeCreditSpecialSales) then begin
                  totcredit = totcredit + IVr.Sum4;
                end;
              end;
            end;
          end;
          if (cntermprc>0) then begin
            totcreditallowed = (orgIVr.Sum4*cntermprc)/100;
          end else begin
            totcreditallowed = orgIVr.Sum4 + (orgIVr.Sum4*cntermprc)/100;
          end;
          if (totcredit>totcreditallowed) then begin
            res = 1726;
            errstr = " " & totcreditallowed & ". ";
            errstr = errstr & USetStr(1727)  & " " & totcredit - CreditIVp.Sum4;
          end;
        end;
      end;
    end;
  end;
LTestCreditNoteTerms:;  
  TestCreditNoteTerms = res;
  return;
end;

global
function LongInt IVVcRecordDefaults(var record IVVc IVr,record IVVc IV2r,LongInt long3,LongInt long4)
begin
  LongInt res;
  val fr,to1,to2,br1,br2;
  string 255 tstr,tstr1,tstr2;
  record IIBlock IIRec;
  record AccBlock ARAccRec;
  record UserVc USr;
  record CUVc CUr;
  Date blankd,td,td1;
  record IVVc lIVr;
  record CYBlock CYb;
 
  BlockLoad(ARAccRec);      
  BlockLoad(IIRec);
  BlockLoad(CYb);
  IVr.SerNr = -1;
  IVr.InvType = kInvoiceTypeNormal;
  IVr.ARonTR = ARAccRec.ARonTR;
  IVr.InvDate = CurrentDate;
  IVr.TransDate = CurrentDate;
  if (SingleUserMode) then begin
    IVr.SerNr = NextSerNr("IVVc",IVr.TransDate,-1,false,"");
  end;
  IVr.PayDate = blankd;
  IVr.CustCat = "";
  IVr.Prntdf = 0;
  IVr.QTNr = -1;
  IVr.SVONr = -1;
  IVr.OrderNr = -1;
  IVr.RetNr = -1;
  IVr.CredInv = -1;
  IVr.CredMark = "";
  IVr.ExportFlag = 0;
  IVr.OKFlag = 0;
  IVr.InstallmentInv = 0;
  IVr.IntCode = IIRec.IntRate;
  IVr.InclVAT = ARAccRec.BasePriceInclVAT;
  IVr.NoTAXonVAT = ARAccRec.NoTAXonVAT;
  IVr.TotalwoTAX = ARAccRec.TotalwoTAX;
  if (HasLocalization("PRT")) then begin 
    IVr.IPBookVAT = ARAccRec.IPBookVAT;  
    IVr.ThirdPartyBillMark = CYb.OutsourcedtoAccBureaux;
  end;
//  IVr.ARAcc = ARAccRec.ARAcc; this should be taken either from customer category or settings
  IVr.PriceList = "";
  IVr.PayDeal = "";
  IVr.Objects = "";
  GetCurUser(USr);
  IVr.Objects = USr.PersObjx;
  IVr.InvComment = "";
  tstr = IVr.CurncyCode;
  GetFullCurncyRate(tstr,IVr.TransDate,fr,to1,to2,br1,br2);
  IVr.CurncyCode = tstr;
  IVr.FrRate = fr;
  IVr.ToRateB1 = to1; 
  IVr.ToRateB2 = to2;
  IVr.BaseRate1 = br1;
  IVr.BaseRate2 = br2;    
  IVr.CalcFinRef = "";
  IVr.LastRemndr = -1;
  IVr.LastRemDate = blankd;
  IVr.OurContact = USr.CurOurContact;
  IVr.Location = USr.Location;
  IVr.SalesMan = USr.Code;
  GetSalesGroup(IVr.SalesMan,tstr);
  IVr.SalesGroup = tstr;
  IVr.Sum0 = blankval;
  IVr.Sum1 = blankval;
  IVr.Sum2 = blankval;
  IVr.Sum3 = blankval;
  IVr.Sum4 = blankval;
  IVr.DiscSum = blankval;
  IVr.DiscPerc = blankval;
  IVr.TotGP = blankval;
  IVr.RecValue = blankval;
  IVr.RetValue = blankval;
  IVr.RetnValue = blankval;  
  IVr.UpdStockFlag = ARAccRec.InvUpdStock;
  if (IsStandardProduct and HasModIN==false) then begin
    IVr.UpdStockFlag = 0;
  end;
  IVr.FrItem = "";
  IVr.OrgCust = "";
  IVr.FrObjects = "";
  IVr.FrBase = blankval;
  IVr.FrPrice = blankval;
  IVr.FrGP = blankval;
  IVr.FrGPPercent = blankval;
  IVr.BaseSum4 = blankval;
  IVr.TotQty = blankval;
  IVr.TotWeight = blankval;
  IVr.TotVolume = blankval;            
  IVr.Commision = blankval;      
  IVr.AuthorizationCode = "";
  IVr.OfficialSerNrSerie = "";
  IVr.Invalid = 0;
  CUr.Code = IVr.CustCode;
  if (ReadFirstMain(CUr,1,true)) then begin
    if (CUr.IntRate!=0) then begin IVr.IntCode = CUr.IntRate; end;
  end;
  IVr.MachineName = CurMachineName;
  IVr.TerminalID = CurTerminalID;
  IVr.BranchID = CurBranchID;
  IVr.DrawerCode = CurDrawerCode(IVr.MachineName);
  tstr = GetObjectFromRestIT(IVr.MachineName);
  if ((nonblank(IVr.Objects)) and (nonblank(tstr))) then begin
    if (SetInSet(tstr,IVr.Objects)==false) then begin
      IVr.Objects = IVr.Objects & ",";
    end;
  end;
  IVr.Objects = IVr.Objects & tstr;
  IVr.FiscalDeviceSeqNr = -1;
  if (SingleUserMode) then begin
    if (IsStandardProduct) then begin
      if (HasLocalization("POL")) then begin
        lIVr.OfficialSerNr = "ZZZZZZZZZZZZZZZZZZZ";
        if (ReadLastKey("OfficialSerNr",lIVr,1,false)) then begin
          FindNextIVVcOfficialSerialNr(IVr);
          if (blank(IVr.OfficialSerNr)) then begin
            NextM4Number(lIVr.OfficialSerNr,IVr.OfficialSerNr);
          end;
        end;
        if (blank(IVr.OfficialSerNr)) then begin
          IVr.OfficialSerNr = "1";
        end;
      end;
    end;
  end;    
  IVr.AcceptanceStatus = kAcceptanceStateNotRequired;
  if (AcceptanceRulesExists(kAcceptanceIV,IVr.CustCode)) then begin
    IVr.AcceptanceStatus = kAcceptanceStateNotStarted;
  end;
  IVr.Status = kRecordStatusNone;
  IVVcRecordDefaults = res;  
  RETURN;
END;

global
procedure IVVcRecordDuplicateDo(var record IVVc IVr,Boolean refreshloyaltydata,Boolean warningf)
BEGIN
  string 255 tstr;
  record UserVc USr;
  Date blankd;
  val fr,to1,to2,br1,br2;
  Integer i,rwcnt;
  row IVVc IVrw;
  val fiforowval,fifo;
  record ModuleBlock OptFeatRec;
  record CUVc CUr;
  record INVc INr;
  record CreditLimitBlock CLb;
  Boolean limitdaysf;
  val bal,limit;
  record IVVc lIVr;
  record AccBlock ARAccRec;
  record CYBlock CYb;
 
  BlockLoad(CYb); 
  BlockLoad(ARAccRec);
  BlockLoad(OptFeatRec);
  IVr.SerNr = -1;
  IVr.InvDate = CurrentDate;
  IVr.TransDate = CurrentDate;
  if (HasLocalization("PRT")) then begin 
    IVr.IPBookVAT = ARAccRec.IPBookVAT;
    IVr.ThirdPartyBillMark = CYb.OutsourcedtoAccBureaux;
  end;
  if (SingleUserMode) then begin
    IVr.SerNr = NextSerNr("IVVc",IVr.TransDate,-1,false,IVr.LangCode);
  end;  
  IVr.ServiceDelDate = blankd;
  IVr.QTNr = -1;
  IVr.Prntdf = 0;
  IVr.SVONr = -1;
  IVr.OrderNr = -1;
  IVr.RetNr = -1;
//  IVr.CredInv = -1; // why ? 
//  IVr.CredMark = "";
  IVr.TaxAuthID = "";
  IVr.TaxAuthIDCC = "";
  IVr.OKFlag = 0;
  IVr.DisputedFlag = 0;
  GetCurUser(USr);
  IVr.OurContact = USr.CurOurContact;
  IVr.CalcFinRef = "";
  PastePDInInv(IVr,"",false);
  IVr.LastRemndr = -1;
  IVr.LastRemDate = blankd;
  tstr = IVr.CurncyCode;
  GetFullCurncyRate(tstr,IVr.TransDate,fr,to1,to2,br1,br2);
  IVr.CurncyCode = tstr;
  IVr.FrRate = fr;
  IVr.ToRateB1 = to1; 
  IVr.ToRateB2 = to2;
  IVr.BaseRate1 = br1;
  IVr.BaseRate2 = br2;  
  IVr.DiscSum = blankval;
  IVr.DiscPerc = blankval;
  IVr.RecValue = blankval;
  IVr.RetValue = blankval;
  IVr.LocOKNr = blankval;
  IVr.RetnValue = blankval;      
  IVr.Invalid = 0;
  IVr.CredManNr = -1;
  IVr.VINr = -1;  
  IVr.Invalid = 0;
  IVr.InvalidDate = "";
  IVr.InvalidTime = "";
  IVr.InvalidBy = "";
  IVr.PlanSendDate = blankd;
  IVr.PlanArrDate = blankd;
  IVr.PlanSendTime = "";
  IVr.PlanArrTime = "";
  IVr.TaxAdminServSignatureTimeStamp = "";      
  IVr.TaxAdminServCertificateNr = "";      
  IVr.TaxAdminServSeal = "";      
  IVr.TaxAdminReceiptNr = "";      
  IVr.TaxAuthReceiptDate = "";      
  IVr.TaxAuthReceiptTime = "";      
  IVr.OfficialSerNrSerie = "";
  rwcnt = MatRowCnt(IVr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(IVr,i,IVrw);
    switch (IVrw.stp) begin
/*
no way of making credit notes ? 
      case kInvoiceRowTypeDownpayment:    
        MatRowDelete(IVr,i);
        i = i-1;
        if (IVrw.VoidedRowNr>=0) then begin
          MatRowDelete(IVr,IVrw.VoidedRowNr);     
          i = i-1;
        end;
*/
      case kInvoiceRowTypeVoid:    
        MatRowDelete(IVr,i);
        i = i-1;
        if (IVrw.VoidedRowNr>=0) then begin
          MatRowDelete(IVr,IVrw.VoidedRowNr);
          i = i-1;
        end;
      case kInvoiceRowTypeLoyaltyPointsPayment:    
        MatRowDelete(IVr,i);
        i = i-1;
      case kInvoiceRowTypeGiftVoucherPayment:    
        MatRowDelete(IVr,i);
        i = i-1;
      case kInvoiceRowTypeCashPayment:    
        MatRowDelete(IVr,i);
        i = i-1;
      case kInvoiceRowTypeCreditCardPayment:    
        MatRowDelete(IVr,i);
        i = i-1;
      otherwise
        fifo = IVrw.FIFO;
        fiforowval = IVrw.FIFORowVal;
        IVrw.FIFO = blankval;
        IVrw.FIFORowVal = blankval;
        IVrw.OrdNr = -1;
        IVrw.RetRow = -1;
        if (OptFeatRec.PUFromCredUpdStock!=0) then begin//special feature for Lith
          IVrw.FIFO = fifo;
          IVrw.FIFORowVal = fiforowval;
        end;
        IVrw.AuthorizationCode = "";
        if (IVr.InvType==kInvoiceTypeCredit or IVr.InvType==kInvoiceTypeCreditSpecialSales) then begin
          if (IVrw.CreditedRow<0) then begin
            IVrw.CreditedRow = i;//cleared on save, update
          end;
        end;
        if (IVr.InvType!=kInvoiceTypeCredit) and (IVr.InvType!=kInvoiceTypeCreditSpecialSales) then begin
          if (IVrw.stp==kInvoiceRowTypeNormal) or (IVrw.stp==kInvoiceRowTypeStructuredItemComponent) or (IVrw.stp==kInvoiceRowTypeCorrection) then begin
            IVrw.BasePrice = GetINCostPrice(IVr.PriceList,IVrw.ArtCode,true);
          end;
        end;
        MatRowPut(IVr,i,IVrw);
        if (warningf) then begin
          if (ReadFirstItem(IVrw.ArtCode,INr,true,false)) then begin
          end;
          if (INr.Terminated!=0) then begin
            MessageBox(1266,INr.Code);
          end;
        end;
    end;
  end;

  IVr.AuthorizationCode = "";
  IVSumup(IVr,true);            
  IVOpenPrepExists(IVr.CustCode);
  IVr.MachineName = CurMachineName;
  IVr.TerminalID = CurTerminalID;
  IVr.BranchID = CurBranchID;
  IVr.DrawerCode = CurDrawerCode(IVr.MachineName);
  tstr = GetObjectFromRestIT(IVr.MachineName);
  if ((nonblank(IVr.Objects)) and (nonblank(tstr))) then begin
    if (SetInSet(tstr,IVr.Objects)==false) then begin
      IVr.Objects = IVr.Objects & ",";
    end;
  end;
  if (SetInSet(tstr,IVr.Objects)==false) then begin
    IVr.Objects = IVr.Objects & tstr;
  end;
  CUr.Code = IVr.CustCode;
  if (ReadFirstMain(CUr,1,true)) then begin
  end;
  if (warningf) then begin
    BlockLoad(CLb);
    if (nonblank(CUr.WarnText1)) then begin
      MessageBox(0,CUr.WarnText1);
    end;
    if (GetCustAndBal(CUr,limit,bal,CLb.Base,CLb.OwnCheques,CLb.ThirdCheques,CLb.IOUCheques,CLb.ThirdIOUCheques,CLb.ORSaveWarn,limitdaysf)) then begin
      if (CLb.IVPaste==1) then begin
        if (limitdaysf) then begin
          MessageBox(0,USetStr(22260));
        end;
        if (limit>0) then begin
          if (bal>limit) then begin
            MessageBox(0,USetStr(1164));
          end;
        end;
      end;
    end;
  end;
  IVr.OfficialSerNr = "";
  IVr.OfficialSerNr2 = "";
  if (SingleUserMode) then begin
    if (IsStandardProduct) then begin
      if (HasLocalization("POL")) then begin
        lIVr.OfficialSerNr = "ZZZZZZZZZZZZZZZZZZZ";
        if (ReadLastKey("OfficialSerNr",lIVr,1,false)) then begin
          FindNextIVVcOfficialSerialNr(IVr);
//          NextM4Number(lIVr.OfficialSerNr,IVr.OfficialSerNr);
        end;
      end;
    end;
  end;
  IVr.CAE = "";
  IVr.CAEAFlag = 0;
  IVr.CAEExpiry = blankd;
  IVr.CAEAStartDate = blankd;
  IVr.CAEAExpiryDate = blankd;
  IVr.Hash = "";
  IVr.HashKeyVersion = 0;
  IVr.AcceptanceStatus = kAcceptanceStateNotRequired;
  if (AcceptanceRulesExists(kAcceptanceIV,IVr.CustCode)) then begin
    IVr.AcceptanceStatus = kAcceptanceStateNotStarted;
  end;
  WarnFutureDate(true,IVr.TransDate);
  WarnFutureDate(true,IVr.InvDate);
  if (TouchScreenInterface) then begin
    IVr.FiscalFlag = 0;
  end;
  IVr.FiscalDeviceSeqNr = -1;
  if (refreshloyaltydata) then begin 
    IVr.LoyaltyCardNr = "";
    IVr.LCMLevel = "";
    IVr.Points = 0;
  end;
  IVr.Status = kRecordStatusNone;
  IVr.FInvExpFlag = 0;
  IVr.PrintedFlag = 0; //Edit***************************Sasha2,10:47 11.01.2016
  IVr.PrintedSum4 = 0; //Edit***************************Sasha2,10:47 11.01.2016
  RETURN;
END;
  
global
function LongInt IVVcRecordDuplicate(var record IVVc IVr,record IVVc IV2r,LongInt long3,LongInt long4)
begin
  LongInt res;

  IVVcRecordDuplicateDo(IVr,false,true);
  IVVcRecordDuplicate = res;
  RETURN;
END;

global
function LongInt IVVcRecordInIndex(record IVVc IVr,string indexname)
begin
  LongInt res;
  
  res = 1;
  if (IVr.OKFlag!=0) or (IVr.Invalid!=0) then begin 
    if (indexname=="OpenSerNr")  then begin res = 0; end;
    if (indexname=="OpenInvDate")  then begin res = 0; end;
    if (indexname=="OpenCustCode")  then begin res = 0; end;
    if (indexname=="OpenName")  then begin res = 0; end;
  end;
  if (IVr.Invalid!=0) then begin 
    if (indexname=="OpenOfficialSerNr2")  then begin res = 0; end;
  end;
  IVVcRecordInIndex = res;
  return;
end;

global
updating procedure IVVc_LinkRecords(record IVVc IVr,string rlinkfilename,string rlink)
begin
  Integer compnr;
  record CheckVc Checkr;
  
  switch (rlinkfilename) begin
    case "CheckVc":
      compnr = UnpackRecordLink(rlink,Checkr);
      CreateRecordLink(Checkr,CurrentCompany,IVr,CurrentCompany);  
      CreateRecordLink(IVr,CurrentCompany,Checkr,CurrentCompany);  
  end;
  return;
end;